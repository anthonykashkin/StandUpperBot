buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:2.0.1"
    }
}

group 'org.protei.sorm.bot'
version '1.0.5-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}
dependencies {
    compile(
            //Telegram API
            [group: 'org.telegram', name: 'telegrambots', version: '3.5'],

            /* Spring */
            [group: 'org.springframework', name: 'spring-context', version: '4.3.12.RELEASE'],

            //Logger
            [group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.6']
    )

    testCompile(
            [group: 'junit', name: 'junit', version: '4.12']
    )

}

task fatJar(type: Jar) {
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
    manifest {
        attributes (
                "Built-By": "anthonykashkin",
                'Implementation-Title': 'Stand Upper Bot',
                'Implementation-Version': version,
                'Main-Class': 'com.organization.project.package.mainClassName',
        )
    }
}

shadowJar {
    destinationDir = file("$buildDir/dist")

    manifest {
        attributes(
                "Built-By": "anthonykashkin",
                "Main-Class": 'org.protei.sorm.bot.Main',
                "Created-By": "gradle ${gradle.gradleVersion}, jdk ${System.getProperty("java.version")}",
                "Specification-Title": project.name,
                "Implementation-Title": "Stand Up Bot"
        )
    }
    classifier = "fatjar"
}

task copyScripts(type: Copy, dependsOn: 'shadowJar') {
    into "$buildDir/dist"
    from "env"
}

task buildDist(type: Tar, dependsOn: 'copyScripts') {
    File file = new File("$buildDir/distribution")
    file.mkdir()
    destinationDir = file
    compression = Compression.GZIP
    from "$buildDir/dist"
}

build.dependsOn 'buildDist'
